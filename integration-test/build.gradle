buildscript {
    ext {
        kotlinVersion = '1.3.10'
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8

repositories { mavenCentral() }

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    compile project(':infrastructure:configuration')

    compile "com.github.tomakehurst:wiremock:2.6.0"
    compile "org.awaitility:awaitility:3.0.0"

    compile group: 'org.eclipse.jetty', name: 'jetty-io', version: '9.4.14.v20181114'


    testCompile "org.testcontainers:testcontainers:1.4.3"
    testCompile "org.testng:testng:6.14.3"

}

// -- integration test task -- //
sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/kotlin')
        }
        resources.srcDir file('src/test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    useTestNG {
        suites 'src/test/resources/testng.xml'
    }
}

check.dependsOn integrationTest
// -- integration test task -- //

compileIntegrationTestKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

integrationTest {

    useTestNG()
    include '**/*IT.*'

    // make gradle print the test result for each test in the build (we like to see the acceptance tests running)
    testLogging {
        events "passed", "skipped", "failed"
    }

    doFirst {
        println "==========================================================================================="
        println ">>> integration-test starting"
        println "==========================================================================================="
    }


    doLast {
        println "==========================================================================================="
        println ">>> integration-test finishing"
        println "==========================================================================================="
    }
}

integrationTest.mustRunAfter(":infrastructure:dataprovider:test")